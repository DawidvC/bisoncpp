%scanner scanner/scanner.h
%token NR
%left '+' '-'
%left '*' '/'
%right UMIN

%%

lines:
    lines line
|
    {
        cout << "? " << flush;
    }
;

line:
    content
    '\n'
    {
        cout << "? " << flush;
    }
;

content:
    expr
    {
        cout << "Expr = " << $1 << "\n";
    }
|
    error
    {
        cout << "Error - try again\n";
    }
|
    'q'
    {
        cout << "Bye\n";
        ACCEPT();
    }
|
    // empty
;

expr:
    NR
    {
        $$ = atoi(d_scanner.YYText());
    }
|
    '(' expr ')'
    {
        $$ = $2;
    }
|
    '-' expr %prec UMIN
    {
        $$ = -$2;
    }
|
    expr '+' expr
    {
        $$ = $1 + $3;
    }
|
    expr '-' expr
    {
        $$ = $1 - $3;
    }
|
    expr '*' expr
    {
        $$ = $1 * $3;
    }
|
    expr '/' expr
    {
        $$ = $1 / $3;
    }
;

